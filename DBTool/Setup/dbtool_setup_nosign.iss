; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "DBTool"
#define MyAppVersion "1.0"
#define MyAppPublisher "HickelSOFT Huth GmbH"
#define MyAppURL "https://www.hickelsoft.de/dbtool/"

[Setup]
; NOTE: The value of AppId uniquely identifies this application. Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
; Hinweis: Die geschweiften Klammern sind so genau richtig! {{...} , siehe jrsoftware.org/ishelp/index.php?topic=consts
AppId={{d90c5592-2277-43af-8ee3-d0cb0692af97}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
;DefaultDirName=C:\HS-Service
DefaultDirName={autopf}\HickelSOFT\{#MyAppName} {#MyAppVersion}
DisableProgramGroupPage=yes
LicenseFile=lizenzbedingungen.rtf
; Uncomment the following line to run in non administrative install mode (install for current user only.)
;PrivilegesRequired=lowest
OutputBaseFilename=DBTool_Setup
OutputDir=.
;OutputDir=\\SHS\Hotline\DBTool
PrivilegesRequiredOverridesAllowed=dialog
UsePreviousPrivileges=no
ShowLanguageDialog=no
WizardStyle=modern
WizardSmallImageFile=..\Private\Export Mini Graphic.bmp
SetupIconFile=..\Private\MAINICON.ico
UninstallDisplayIcon={app}\DBTool32.exe
; 64 Bit Support:
ArchitecturesAllowed=x86compatible x64compatible
ArchitecturesInstallIn64BitMode=x64compatible

[Languages]
Name: en; MessagesFile: "compiler:Default.isl"
Name: de; MessagesFile: "compiler:Languages\German.isl"

[Tasks]
; DM entfernt: Flags: unchecked
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"
Name: "firebirdclient"; Description: "Firebird Client (FB 3.x / 4.x / 5.x) installieren"; GroupDescription: "Optionale Komponenten"; Flags: checkedonce
Name: "bde"; Description: "Borland Database Engine 5.2 installieren"; GroupDescription: "Optionale Komponenten"; Flags: checkedonce

[Files]
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

; Die Dinger werden von Delphi kompiliert
Source: "C:\HS-Service\DBTool\DBTool32.exe"; DestDir: "{app}"; DestName: "DBTool32.exe"; Flags: ignoreversion
Source: "C:\HS-Service\DBTool\DBTool64.exe"; DestDir: "{app}"; DestName: "DBTool64.exe"; Flags: ignoreversion; Check: Is64BitInstallMode
Source: "C:\HS-Service\DBTool\DBTool32.enu"; DestDir: "{app}"; DestName: "DBTool32.enu"; Flags: ignoreversion
Source: "C:\HS-Service\DBTool\DBTool64.enu"; DestDir: "{app}"; DestName: "DBTool64.enu"; Flags: ignoreversion; Check: Is64BitInstallMode

; https://www.firebirdsql.org/en/firebird-3-0
Source: "C:\HS-Service\DBTool\FB30_32\*.*";  DestDir: "{app}\FB30_32"; Flags: ignoreversion recursesubdirs; Tasks: firebirdclient
Source: "C:\HS-Service\DBTool\FB30_64\*.*";  DestDir: "{app}\FB30_64"; Flags: ignoreversion recursesubdirs; Tasks: firebirdclient; Check: Is64BitInstallMode

; https://www.firebirdsql.org/en/firebird-4-0
Source: "C:\HS-Service\DBTool\FB40_32\*.*";  DestDir: "{app}\FB40_32"; Flags: ignoreversion recursesubdirs; Tasks: firebirdclient
Source: "C:\HS-Service\DBTool\FB40_64\*.*";  DestDir: "{app}\FB40_64"; Flags: ignoreversion recursesubdirs; Tasks: firebirdclient; Check: Is64BitInstallMode

; https://www.firebirdsql.org/en/firebird-5-0
Source: "C:\HS-Service\DBTool\FB50_32\*.*";  DestDir: "{app}\FB50_32"; Flags: ignoreversion recursesubdirs; Tasks: firebirdclient
Source: "C:\HS-Service\DBTool\FB50_64\*.*";  DestDir: "{app}\FB50_64"; Flags: ignoreversion recursesubdirs; Tasks: firebirdclient; Check: Is64BitInstallMode

; Restliche Dateien liegen hier im Setup-Verzeichnis
Source: "lizenzbedingungen.rtf"; DestDir: "{app}"; Flags: ignoreversion

; BDE
Source: "BDE_Setup.exe"; DestDir: "{tmp}"; Tasks: bde; Flags: ignoreversion deleteafterinstall

[Run]
Filename: "{tmp}\BDE_Setup.exe"; Parameters: "/SILENT"; StatusMsg: "Starte BDE Setup..."; Flags: waituntilterminated; Tasks: bde

[UninstallDelete]
Type: files; Name: "{app}\Setup.ini"

[Icons]
Name: "{autoprograms}\{#MyAppName} {#MyAppVersion} (32 Bit + BDE)"; Filename: "{app}\DBTool32.exe"; Check: Is64BitInstallMode
Name: "{autoprograms}\{#MyAppName} {#MyAppVersion} (64 Bit)"; Filename: "{app}\DBTool64.exe"; Check: Is64BitInstallMode
Name: "{autoprograms}\{#MyAppName} {#MyAppVersion}"; Filename: "{app}\DBTool32.exe"; Check: not Is64BitInstallMode

Name: "{autodesktop}\{#MyAppName} {#MyAppVersion} (32 Bit + BDE)"; Filename: "{app}\DBTool32.exe"; Tasks: desktopicon; Check: Is64BitInstallMode
Name: "{autodesktop}\{#MyAppName} {#MyAppVersion} (64 Bit)"; Filename: "{app}\DBTool64.exe"; Tasks: desktopicon; Check: Is64BitInstallMode
Name: "{autodesktop}\{#MyAppName} {#MyAppVersion}"; Filename: "{app}\DBTool32.exe"; Tasks: desktopicon; Check: not Is64BitInstallMode

;[Run]
; skipifsilent nicht verwenden, wenn man /RESTARTAPPLICATIONS verwenden möchte
;Filename: "{app}\DBTool32.exe"; Description: "{cm:LaunchProgram,{#StringChange(MyAppName, '&', '&&')}}"; Flags: nowait postinstall

[Code]

procedure CurStepChanged(CurStep: TSetupStep);
var
  ownHash: string;
begin
  if CurStep=ssPostInstall then
  begin
    ownHash := GetMD5OfFile(ExpandConstant('{srcexe}'));

    // Wir merken uns den MD5-Hash vom installierten Setup, sodass wir wissen,
    // ob das Update OK war und wann wir wieder ein neues Update brauchen
    SetIniString('Setup', 'InstalledMD5', ownHash, ExpandConstant('{app}\Setup.ini'));
  end;
end;